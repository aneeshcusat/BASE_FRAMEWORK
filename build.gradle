// project version
version = "$applicationVersion"
apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'com.bmuschko.tomcat'
apply plugin: 'groovy'
apply plugin: 'eclipse'

// JDK version source compatibility
sourceCompatibility = 1.7
targetCompatibility = 1.7

// War file name
war.baseName = "$warBaseName"

// Web directory, this overrides the default value "webapp"
project.webAppDirName = 'WebApp'


buildscript {
	repositories {
		jcenter()
	}

	dependencies {
		classpath 'com.bmuschko:gradle-tomcat-plugin:2.0'
	}
}

// dependencies to run on tomcat, are mandatory for tomcat plugin
dependencies {
	compile 'org.slf4j:slf4j-api:1.7.21'
	
	compile 'org.springframework:spring-aop:2.5.6.SEC03'
	compile 'org.springframework:spring-asm:3.0.5.RELEASE'
	compile 'org.springframework:spring-beans:3.0.5.RELEASE'
	compile 'org.springframework:spring-context:3.0.5.RELEASE'
	compile 'org.springframework:spring-core:3.0.5.RELEASE'
	compile 'org.springframework:spring-expression:3.0.5.RELEASE'
	compile 'org.springframework:spring-web:3.0.5.RELEASE'
	
	providedCompile "javax.servlet:servlet-api:2.5"
	
	compile 'com.sun.jersey.contribs:jersey-spring:1.8'
	compile 'com.sun.jersey:jersey-core:1.8'
	compile 'com.sun.jersey:jersey-server:1.8'
	
	def tomcatVersion = '7.0.50'
	
	tomcat "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}",
			"org.apache.tomcat.embed:tomcat-embed-logging-juli:${tomcatVersion}"
	
	tomcat("org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}") {
		exclude group: 'org.eclipse.jdt.core.compiler', module: 'ecj'
	}
	
	testCompile(
            'junit:junit:4.10',
            'org.codehaus.groovy:groovy-all:1.8.6',
            'org.spockframework:spock-core:0.7-groovy-1.8',
    )
    
    testRuntime(
            'com.athaydes:spock-reports:1.2.7'
    )
}

repositories {
	mavenLocal()
	mavenCentral()
	maven {
		url "http://central.maven.org/maven2/"
	}
	jcenter()
}

task explodedWar(type: Copy) {
	into "$buildDir/explodedWar"
	with war
}

war.dependsOn explodedWar

// Set source directory
sourceSets {
		main {
			java {
				srcDir 'Base/src/main/java'
				srcDir 'Rest.Services/src/main/java'
			}
		}
		test {
    		groovy {
      			srcDir 'Base/src/test/groovy'
      			srcDir 'Rest.Services/src/test/groovy'
   			}
  		}
	}

// context where tomcat is deployed, by defautl localhost:8080/
tomcatRun.contextPath = '/'
tomcatRunWar.contextPath = '/'

task wrapper(type: Wrapper) {
	gradleVersion = '2.4'
}

task sourceSetProperties << {
sourceSets {
	main {
		println "java.srcDirs = ${java.srcDirs}"
		println "resources.srcDirs = ${resources.srcDirs}"
		println "java.files = ${java.files.name}"
		println "allJava.files = ${allJava.files.name}"
		println "resources.files = ${resources.files.name}"
		println "allSource.files = ${allSource.files.name}"
		println "output.classesDir = ${output.classesDir}"
		println "output.resourcesDir = ${output.resourcesDir}"
		println "output.files = ${output.files}"
		}
	}
}
